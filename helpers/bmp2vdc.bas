#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
#THIS CONVERTS BMP FILES 1:1 TO RAW VDC DATA
#  THIS IS INTENDED FOR SCREEN-RAM ONLY. NO ATTRIBUTE-RAM HANDLING (IE NO COLORS)
#  INPUT NEEDS TO BE MONOCHROME (IE 1 BIT PER PIXEL)
#  THAT MEANS: BYTES ARE IN THE RIGHT ORDER (TOP-LEFT TO BOTTOM-RIGHT)
#  AND THE COLOR INFORMATION IS STORED CORRECTLY

# 4.64 MINUTES WRITING TO VRAM
# 4.16 MINUTES POKING AND THEN WRITING TO VRAM

10 GRAPHIC 0
# DEFINE FUNCTIONS
20 GOSUB 800
30 BS=FN W(45)
40 BE=FN W(4624)
# DB=DATA BEGIN
# DE=DATA END
50 DB=BE+1:DE=DB+8000

60 PRINT "BASIC FROM "BS" TO "BE"."
70 PRINT "LEAVES "FRE(0)" BYTES FREE."
80 DD=PEEK(186)

90 PRINT "INSERT DATADISK":GETKEY I$

100 BLOAD "VDCBASIC2F.1300",B0,U(DD):SYS DEC("1300")

110 AP=16000

# 160 SCANLINES (EACH VAL +1)
#120 RGW 0,127:RGW 4,155:RGW 6,100:RGW 7,140
130 RGO 25,128:REM BITMAP MODE ON
140 RGO 28,2î¸ž4:REM 64K VRAM
150 RGW 36,0
160 RGW DEC("1A"),DEC("FF")

170 VMF 0,0,AP:REM SETUP PIXELS
180 VMF AP,DEC("0F"),8000: REM CLEAR ATTRIBUTE RAM
190 ATTR AP:BANK 0



210 PF$="TXTCHARSET2.BMP"
220 GOSUB 250


240 BANK15:END




250 PRINT "LOADING "PF$" ... ";
260 OPEN1,DD,0,PF$
270 GET#1,B1$,B2$
280 CLOSE1
290 POKE DB,ASC(B1$):POKEDB+1,ASC(B2$)

300 BLOAD (PF$),B0,P(DB+2),U(DD)
310 PRINT "DONE"
320 H1=PEEK(DB):H2=PEEK(DB+1)
330 IF (CHR$(H1)+CHR$(H2)) = "BM" THEN GOSUB 360

340 SLOW


350 RETURN


##################
# BITMAP ROUTINE #
##################

# HEADER
360 PRINT "BITMAP"
370 IS=FN W(DB+2)

380 PRINT "BFSIZE"IS
390 OS=FN W4(DB+10)
400 PRINT "BFOFFBITS"OS

# INFORMATIONBLOCK
#1030 PRINT "BISIZE"FN W4(DB+14)
410 BW=FN W4(DB+18)
420 PRINT "BIWIDTH"BW
430 BH=FN W4(DB+22)
440 PRINT "BIHEIGHT"BH
450 BC=FN W(DB+28)
460 PRINT "BIBITCOUNT"BC
470 IF BC<=8 THEN PRINT " INDICED COLORS"
480 IF BC >8 THEN PRINT " COLOR DEPTH "BC" NOT SUPPORTED. THE END.":END
490 PRINT "BICOMPRESSION"FN W4(DB+30)
500 P1=FN W4(DB+34):PRINT "BISIZEIMAGE"P1
#1090 PRINT "BIXPELSPERMETER"FN W4(DB+38)
#1100 PRINT "BIYPELSPERMETER"FN W4(DB+42)
510 CU=FN W4(DB+46)
520 PRINT "BICLRUSED"CU
530 PRINT "BICLRIMPORTANT"FN W4(DB+50)

# PALETTE
540 IF CU<>0 THEN PRINT " PALETTE WITH "CU" ENTRIES":GOTO 560
550 IF BC<=8 THEN PRINT " PALETTE WITH "BC" ENTRIES":GOTO 560: ELSE PRINT " NO PALETTE"

# IMAGEDATA
560 BANK0
570 ID=DB+OS
#514 VP=AP+BH*80
580 BW=BW/8
590 P2=P1/BH
600 VP=P1

605 WO=(BW)*(BH-1)
606 WS=0
607 WE=WO+BH

#   RO:READ OFFSET
#   WO:WRITE OFFSET
#   RC=READ COUNT
610 S=TI
620 FOR Y=BH-1 TO 0 STEP -1
640  FOR X=0 TO BW-1

#650   V=PEEK(ID)
#652   PRINT C$:GETKEY I$

#     WRITE TWO PIXELS TO VRAM
670   VMW WO+X,PEEK(ID):VP=VP+1:ID=ID+1
680  NEXT

685  WO=WO-BW

710 NEXT:S=TI-S:SLOW:PRINT "TOOK "S" JIFFIES. KEY TO CONT."

715 PRINT C$:GETKEY I$

#   COPY FROM VRAM TO RAM
720 FL=WE-WS
730 VTR 0,DB+IS+2,FL

#   COPY FROM INVISIBLE TO VISIBLE VRAM (FOR VISUAL VALIDATION)
740 VMC 0,32768,BW,BH,80
745 DISP 32768

750 POKE DB+IS,BW:POKE DB+IS+1,BH

760 WF$="@"+LEFT$(PF$,LEN(PF$)-4)+".VDC"
770 BSAVE (WF$),B0,U(DD),P(DB+IS) TO P(DB+IS+FL+2)

780 RETURN





#620 FAST:BANK0:ID=DB+OS:VP=DB+IS+BH*80:BW=BW/2-1
#630 S=TI:FOR Y=BH-1 TO 0 STEP -1
#640  FOR X=0 TO BW
#650   IF BC=4 THEN C1$=MID$(HEX$(PEEK(ID)),3,1):C2$=RIGHT$(HEX$(PEEK(ID)),1):ID=ID+1:GOTO 670
#660   IF BC=8 THEN C1$=RIGHT$(HEX$(PEEK(ID)),1):ID=ID+1:C2$=RIGHT$(HEX$(PEEK(ID)),1):ID=ID+1
#670   POKE VP,DEC(C1$+C2$):VP=VP+1
#680  NEXT
#690  VP=VP-160

#700 NEXT
#710 RTV DB+IS,AP,(BW+1)*BH:S=TI-S:PRINT S

#720 RETURN




790 RETURN

800 DEF FN W(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
810 DEF FN W4(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256+PEEK(ZZ+2)*65536+PEEK(ZZ+3)*16777216
820 DEF FN V(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))
830 DEF FN HN(ZZ)=DEC(MID$(HEX$(ZZ),3,1))
840 DEF FN LN(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))

850 RETURN