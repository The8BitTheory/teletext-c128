#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
10 GRAPHIC0:DD=PEEK(186)
15 PRINT "INSERT DISK AND PRESS KEY TO CONTINUE":GETKEY I$

20 PRINT"LOADING VDCBASIC...";:BLOAD"VDCBASIC2G.1300",U(DD),B0:PRINT"DONE"
22 SYS DEC("1300")

30 ::SF=RGD(255)
35 IF SF=0 THEN PRINT "64KB VRAM BENOETIGT, SORRY.":END

# REG 4*REG 9 = 320
# 35*9=315

# 4: TOTAL NR OF SCREEN ROWS (DEFAULT: 39 FOR PAL) -> 35
# 5: VERT FILLER: 5
# 6: VISIBLE NR OF SCREEN ROWS (DEFAULT: 25). WE WANT 26?
# 9: SCANLINES PER CHARACTER: (DEFAULT: 7) WE WANT 9 (VALUE 8). NEEDED FOR ATTRIBUTE-RAM.

40 ::RGO 28,16:REM 64K VRAM
42 ::RGO 25,128:REM BITMAP MODE ON

44 ::RGW 4,35
46 ::RGW 5,5
48 ::RGW 9,8

50 ::RGW 36,0
52 ::DISP 0:ATTR 18000

#  CLEAR SCREEN
60 GOSUB 500

# I$() HOLDS THE THREE DIGITS FOR THREE PAGES
# IX HOLDS THE CURRENTLY ENTERED DIGIT (0-2)
80 DIM I$(2):I$(0)="1":I$(1)="0":I$(2)="0":IX=0

# IZ:INPUT LOW (MIN ALLOWED VALUE)
# IO:INPUT ONE (MIN ALLOWED VALUE FOR FIRST DIGIT)
# IH:INPUT HIGH (MAX ALLOWED VALUE)
85 IZ=ASC("0"):IH=ASC("9"):IO=ASC("1")

100 BE=PEEK(4624)+PEEK(4625)*256:ML=DEC("2400")

#   LOAD CHARSET AND COPY IT TO VRAM
110 BLOAD"TXTCHARSET3.VDC",B0,P(BE)
120 FL=PEEK(174)+PEEK(175)*256-BE
130 RTV BE,56*1024,FL
140 VCS 56*1024,2,9,80

200 BLOAD"C128TEXT.BIN",B0

#   INIT MAKES SURE TO WRITE IMPORTANT MEMORY ADDRESSES TO $FD
210 SYS ML+6:C0=PEEK(DEC("FD"))+PEEK(DEC("FE"))*256:PRINT
215 PRINT "CONTENT AT "HEX$(C0)

#   GET IP
#220 SYS ML+3

#   REQUEST PAGE 100
300 VMP 10,"HOLE SEITE..."
303 FAST
304 S=TI
305 SYS ML,ASC(I$(0)),ASC(I$(1)),ASC(I$(2))
307 S=TI-S:PRINT "DOWNLOAD AND PARSE TOOK "S" JIFFIES"

#   CLEAR SCREEN
310 GOSUB 500
320 FOR X=0 TO 2
322  ::VMP X+X,I$(X)
324 NEXT

#   DISPLAY LAST REQUESTED PAGE
#   PRINT LINES TO VRAM
#   COPY ATTRIBUTES TO VRAM
330 P=720:CT=C0:S=TI
335 FOR X=0TO23
340  ::VMB P,CT,39
345  CT=CT+39:P=P+720
350 NEXT

# COPY 1000 BYTES OF ATTRIBUTE-RAM TO VRAM
355 ::RTV C0+1024,40000,1000

360 AS=40000:AD=18080

# COPY ATTRIBUTE-RAM. WE HAVE TO DOUBLE EACH BYTE
#  COPY FROM C0+1024 TO 18080, 1 BYTE, REPEAT 250 TIMES, TARGET ADDRESS INCR 2
#  COPY FROM C0+1024 TO 18081, 1 BYTE, REPEAT 250 TIMES, TARGET ADDRESS INCR 2
#  DO THIS 4 TIMES, INCREASING SOURCE AND TARGET ADDRESSES BY 250

370 FOR X=0 TO 22
375  VMC AS,AD,1,39,2:VMF AD+79,0,2
380  VMC AS,AD+1,1,39,2
385  AS=AS+39:AD=AD+80
390 NEXT


395 S=TI-S:SLOW
399 PRINT "COPY TO VRAM TOOK "S" JIFFIES"

#   GET USER INPUT
400 GETKEY I$

#   IL = LOWEST ALLOWED INPUT VALUE

415 I=ASC(I$):IF IX=0 THEN IL=IO: ELSE IL=IZ

#   DIGITS. AFTER 600, GOES TO EITHER 400 (MORE DIGITS) OR 300 (LOAD PAGE)
420 IF I>=IL AND I<=IH THEN GOTO 600

450 IF I$="X" THEN END
460 IF I$="R" THEN 300
470 IF I$="F" THEN FAST:GOTO 400
475 IF I$="S" THEN SLOW:GOTO 400

498 PRINT CHR$(7);
499 GOTO 400

################
# CLEAR SCREEN #
################
# FIRST LINE (WHITE TEXT ON BLACK BACKGROUND)
500 ::VMF 0,255,720
510 ::VMF 18000,DEC("F0"),80

#  BODY OF PAGE (LINES 1-25)
520 ::VMF 720,0,18000-720
530 ::VMF 18080,DEC("0F"),1920

599 RETURN


######################
# HANDLE DIGIT INPUT #
######################

600 I$(IX)=I$
#610 PRINT IX":"I$"/"I
610 ::VMP IX+IX,I$
620 ::VMF 18000+IX+IX,DEC("B0"),2
630 IX=IX+1

#   LESS THAN 3 DIGITS, WAIT FOR NEXT INPUT
640 IF IX<=2 THEN 400
#   3 DIGITS ENTERED, LOAD PAGE
650 IF IX>2 THEN IX=0:GOTO 300
